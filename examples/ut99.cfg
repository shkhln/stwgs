//TODO: use joystick for weapon selection

def rad(deg) = deg * 3.14159265358979323846264338327950288 / 180.0;

let left_pad  = merge(input(LPadX), input(LPadY));
let right_pad = merge(input(RPadX), input(RPadY));

def dpad_button(pad, direction) =
  pad.as_ring_sector_button(
    direction    = rad(direction),
    angle        = rad(120/*deg*/),
    inner_radius = 0.25,
    outer_radius = 1.2,
    margin       = 0.1);

let sensitivity = 1.25;
let smoothing   = 8.0;

def pad_as_mouse(pad, touch, enable_btn) = {
  let x, y = split(pad);
  x.scale( 1920 * sensitivity).relative(touch).smooth(smoothing).gate(enable_btn).bind(Ms.X),
  y.scale(-1080 * sensitivity).relative(touch).smooth(smoothing).gate(enable_btn).bind(Ms.Y)
};

dpad_button(left_pad,  90/*deg*/).bind(Kb.W),
dpad_button(left_pad, 180/*deg*/).bind(Kb.A),
dpad_button(left_pad, -90/*deg*/).bind(Kb.S),
dpad_button(left_pad,   0/*deg*/).bind(Kb.D),

input(LPadPress).bind(Kb.Space),

pad_as_mouse(right_pad, input(RPadTouch), input(RPadPress).invert()),

input(LTrig).as_line_segment_button(from = 0.12, to = 1.0, margin = 0.04).bind(Ms.RB),
input(RTrig).as_line_segment_button(from = 0.12, to = 1.0, margin = 0.04).bind(Ms.LB),

input(Yaw  ).gate(input(RPadTouch)).scale(-0.015).bind(Ms.X),
input(Roll ).gate(input(RPadTouch)).scale(-0.020).bind(Ms.X),
input(Pitch).gate(input(RPadTouch)).scale(-0.015).bind(Ms.Y),

input(LBump).as_axis(value =  1, repeat = false).bind(Ms.Wheel),
input(RBump).as_axis(value = -1, repeat = false).bind(Ms.Wheel),

input(Back ).bind(Kb.Esc),
input(Start).bind(Kb.Enter)
